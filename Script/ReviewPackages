#!/usr/bin/env fish

function main
    for package in (list-non-local-packages)
        echo "Non local package:" $package
        if gum confirm "Mark package '$package' as dependency?"
            sudo --validate
            sudo pacman -D --asdep $package >/dev/null
        end
    end

    if test (count (list-orphan-packages)) = 0
        return
    end

    echo "Orphan packages:"
    for package in (list-orphan-packages)
        echo "- $package"
    end
end

function list-non-local-packages
    for package in (list-packages)
        set -l pkg (string split -f1 '-' $package)
        if test $pkg != local
            echo $package
        end
    end
end

function list-packages
    for package in (pacman -Qe)
        string split -f1 ' ' $package
    end
end

function list-orphan-packages
    for package in (pacman -Qtdq)
        string split -f1 ' ' $package
    end
end

main
exit $status
