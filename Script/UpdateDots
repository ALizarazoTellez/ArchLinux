#!/usr/bin/env fish

set Dotfiles (path resolve (path dirname (status --current-filename))/..)
set DotfilesConfig $Dotfiles/Config
set DotfilesSystemConfig $Dotfiles/System

set Local (path resolve ~)
set LocalConfig $Local/.config

set SystemConfig /

function main
    update-dotfiles $DotfilesConfig $LocalConfig

    update-dotfiles $DotfilesSystemConfig $SystemConfig
end

function update-dotfiles
    set -f dotfiles $argv[1]
    set -f local $argv[2]

    echo "Working on dotifiles: $dotfiles"
    echo "Working on local files: $local"

    for file in $dotfiles/**
        if not test -f $file
            continue
        end

        set -l file (remove-prefix $dotfiles $file)

        cmp -s $local/$file $dotfiles/$file
        switch $status
            case 2
                echo "The file not exists:" $file
                if gum confirm "Delete it?"
                    rm $dotfiles/$file
                end
            case 1
                echo "The file is different:" $file
                if gum confirm "Update dotfile?"
                    cp $local/$file $dotfiles/$file
                end
            case 0
                echo "Ok:" $file
        end
    end
end

# Args: prefix, target
# Print: target without prefix.
function remove-prefix
    string sub --start=(math (string length $argv[1]) + 2) $argv[2]
end

main
exit $status
